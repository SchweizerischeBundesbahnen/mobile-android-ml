apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

apply from: '../version-code.gradle'

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        ndk {
            ndk.abiFilters 'armeabi-v7a','arm64-v8a', 'x86', 'x86_64'
            // 'x86','x86_64', are for emulator due to instrumentation tests
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    aaptOptions {
        noCompress "tflite"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path './CMakeLists.txt'
        }
    }

    kotlinOptions {
        jvmTarget = "11"
    }
}

// see http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaHtml) {
    description 'Generates a JavaDoc archive (.jar) from the JavaDoc files'
    from "$buildDir/dokka"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

afterEvaluate{
    publishing {
        publications {
            maven(MavenPublication)  {
                groupId 'ch.sbb.mobile.ml'
                version PUBLISH_VERSION
                artifactId 'sbbml'
                artifact("$buildDir/outputs/aar/sbbml-release.aar")

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    //Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                artifact androidSourcesJar
                artifact androidJavadocsJar
            }
        }
    }
    signing {
        useInMemoryPgpKeys(
                System.getenv("PGP_KEYID"),
                System.getenv("PGP_KEY"),
                System.getenv("PGP_PASSWORD"),
        )
        sign publishing.publications
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'org.tensorflow:tensorflow-lite:2.7.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.1.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.1.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.7.0'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"

    dokkaHtmlPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:$dokka_version"
}
